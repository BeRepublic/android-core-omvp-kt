apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize "4g"
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        applicationId rootProject.ext.androidApplicationId
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        testApplicationId rootProject.ext.testApplicationId

        // Load values defined in local.properties to BuildConfig.
        Properties properties = new Properties()
        if (project.rootProject.file('local.properties').exists()) {
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            if (!properties.isEmpty()) {
                for (String key : properties.keySet()) {
                    String parsedKey = key.replaceAll("\\.","_").toUpperCase()
                    String parsedValue = "\""+properties.getProperty(key).replaceAll("\\\\","/")+"\""
                    buildConfigField "String", parsedKey, parsedValue
                }
            }
        }

        // Inject build variables into the Manifest
        manifestPlaceholders = [schemaName: rootProject.ext.androidSchemaName]

        // property used to generate name apk [name-productFlavor-buildType.apk]
        setProperty("archivesBaseName", rootProject.ext.androidAppName)

        vectorDrawables.useSupportLibrary = true

        ext.betaDistributionReleaseNotesFilePath = rootProject.ext.betaDistributionReleaseNotesFilePath
        ext.betaDistributionGroupAliasesFilePath = rootProject.ext.betaDistributionGroupAliasesFilePath
        ext.betaDistributionNotifications = rootProject.ext.betaDistributionNotifications

        resValue "string", "facebook_app_id", rootProject.ext.facebookId
        resValue "string", "facebook_login_protocol_scheme", rootProject.ext.facebookSchemeId

        multiDexEnabled true
    }

    flavorDimensions "tier"

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/commons',
                            'src/main/res/layouts/dialog',
                            'src/main/res/layouts/samples/home',
                            'src/main/res/layouts/samples/simple',
                            'src/main/res/layouts/samples/detail',
                            'src/main/res/layouts/samples/list',
                            'src/main/res/layouts/samples/list_horizontal',
                            'src/main/res/layouts/samples/pager',
                            'src/main/res/layouts/samples/multiple',
                            'src/main/res/layouts/samples/locale',
                            'src/main/res/layouts/samples/location',
                            'src/main/res/layouts/samples/take_picture',
                            'src/main/res/layouts/samples/vibration',
                            'src/main/res/layouts/samples/input',
                            'src/main/res/layouts/samples/social',
                            'src/main/res/layouts/samples/notice_dialog',
                            'src/main/res/layouts/samples/bottom_navigation',
                            'src/main/res/layouts/samples/request_phone_flow',
                            'src/main/res/layouts/samples/coroutine',
                            'src/main/res/layouts/splash',
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }
    }

    signingConfigs {
        debug {
            ext.enableCrashlytics = false
            storeFile file('../buildSystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('../buildSystem/release.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'androiddebugkey'
        }
    }

    productFlavors {
        mock {
            dimension "tier"
            applicationIdSuffix ".mock"
            versionNameSuffix "-mock"
            resValue "string", "app_name", rootProject.ext.androidAppName+" Mock"
            resConfigs "xxhdpi"
        }
        dev {
            dimension "tier"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            resValue "string", "app_name", rootProject.ext.androidAppName+" Dev"
            resConfigs "xxhdpi"
        }
        prod {
            dimension "tier"
            resValue "string", "app_name", rootProject.ext.androidAppName
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

    androidExtensions {
        experimental = true
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    rootProject.appDependencies.each{
        add(it.configuration, it.dependency, it.options)
    }
}

apply plugin: 'com.google.gms.google-services'
